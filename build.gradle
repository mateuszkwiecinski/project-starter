import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories.google()
    configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == "org.jetbrains.kotlin") {
                details.useVersion "1.4.0"
                details.because "Runtime JAR files in the classpath should have the same version"
            }
        }
    }
}

plugins {
    id 'com.starter.config' version '0.18.0'
    id 'com.starter.versioning' version '0.17.1'
}

commonConfig {
    javaFilesAllowed false
}

allprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            apiVersion = '1.3'
        }
    }
    configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == "org.jetbrains.kotlin") {
                details.useVersion "1.4.0"
                details.because "Runtime JAR files in the classpath should have the same version"
            }
        }
    }
}
