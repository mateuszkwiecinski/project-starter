import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories.google()

    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'pl.allegro.tech.build' && requested.name.startsWith("axion-release-plugin")) {
                details.useVersion "1.13.1"
            }
        }
    }
}

plugins {
    id 'com.starter.config' version '0.23.0'
    id 'com.starter.versioning' version '0.23.0'
}

commonConfig {
    javaFilesAllowed false
}

allprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            apiVersion = '1.3'
            useIR = true
        }
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'org.jetbrains.kotlin' && requested.name.startsWith("kotlin")) {
                details.useVersion "1.4.32"
            }
        }
    }
}
