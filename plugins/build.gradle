plugins {
    id 'com.gradle.plugin-publish' version '0.13.0'
    id 'java-gradle-plugin'
    id 'com.starter.library.kotlin'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.7'
    id 'com.starter.publishing'
}

repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
}

dependencies {
    api 'org.jmailen.gradle:kotlinter-gradle:3.3.0'
    api 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.15.0'
    api 'com.android.tools.build:gradle:4.1.2'
    api 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.31'
    implementation 'pl.allegro.tech.build:axion-release-plugin:1.12.1'
    implementation 'com.github.usefulness:issuechecker:0.1.1'

    testImplementation "org.assertj:assertj-core:3.19.0"
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.1"
}

jacoco {
    toolVersion = "0.8.6"
}
test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}


pluginBundle {
    website = 'https://github.com/usefulness/project-starter/'
    vcsUrl = 'https://github.com/usefulness/project-starter.git'
    description = 'Set of plugins that might be useful for Multi-Module Android projects.'
    tags = ['android', 'kotlin', 'quickstart', 'codestyle', 'library', 'baseline']
}

gradlePlugin {
    plugins {
        quality {
            id = 'com.starter.quality'
            displayName = 'Code Quality Plugin'
            implementationClass = 'com.project.starter.quality.plugins.QualityPlugin'
        }
        kotlinLibrary {
            id = 'com.starter.library.kotlin'
            displayName = 'Kotlin Library Plugin'
            implementationClass = 'com.project.starter.modules.plugins.KotlinLibraryPlugin'
        }
        androidLibrary {
            id = 'com.starter.library.android'
            displayName = 'Android Library Plugin'
            implementationClass = 'com.project.starter.modules.plugins.AndroidLibraryPlugin'
        }
        androidApplication {
            id = 'com.starter.application.android'
            displayName = 'Android Applicataion Plugin'
            implementationClass = 'com.project.starter.modules.plugins.AndroidApplicationPlugin'
        }
        commonConfig {
            id = 'com.starter.config'
            displayName = 'Common Configuration Plugin'
            implementationClass = 'com.project.starter.config.plugins.CommonSettingsPlugin'
        }
        versioning {
            id = 'com.starter.versioning'
            displayName = 'Versioning Plugin'
            implementationClass = 'com.project.starter.versioning.plugins.VersioningPlugin'
        }
    }
}
