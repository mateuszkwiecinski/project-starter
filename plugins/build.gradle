import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'maven-publish'
    id 'org.jmailen.kotlinter' version '2.2.0'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.4'
    id 'jacoco'
}

repositories {
    jcenter()
    google()
    gradlePluginPortal()
}

tasks.named('jacocoTestReport') {
    dependsOn 'test', 'generateJacocoTestKitProperties'
    reports {
        xml.enabled true
    }
}

tasks.withType(KotlinCompile) {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
}

dependencies {
    api 'org.jmailen.gradle:kotlinter-gradle:2.2.0'
    api 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.3.1'
    api 'com.android.tools.build:gradle:3.5.3'
    implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation 'org.assertj:assertj-core:3.14.0'
}

publishing {
    repositories {
        maven {
            name = 'github'
            setUrl('https://maven.pkg.github.com/mateuszkwiecinski/project-starter')
            credentials {
                username 'mateuszkwiecinski'
                password project.findProperty('GH_TOKEN') ?: ''
            }
        }
    }
    publications {
        register('jar', MavenPublication) {
            from(components.java)
        }
    }
}
pluginBundle {
    website = 'https://github.com/mateuszkwiecinski/project-starter/'
    vcsUrl = 'https://github.com/mateuszkwiecinski/project-starter/'
    description = 'Set of plugins that might be useful for Multi-Module Android projects.'
    tags = ['android', 'kotlin', 'quickstart', 'codestyle', 'library']
}

gradlePlugin {
    plugins {
        quality {
            id = 'com.starter.quality'
            displayName = 'Code Quality Plugin'
            implementationClass = 'com.project.starter.quality.plugins.QualityPlugin'
        }
        kotlinLibrary {
            id = 'com.starter.library.kotlin'
            displayName = 'Kotlin Library Plugin'
            implementationClass = 'com.project.starter.modules.plugins.KotlinLibraryPlugin'
        }
        androidLibrary {
            id = 'com.starter.library.android'
            displayName = 'Android Library Plugin'
            implementationClass = 'com.project.starter.modules.plugins.AndroidLibraryPlugin'
        }
        androidApplication {
            id = 'com.starter.application.android'
            displayName = 'Android Applicataion Plugin'
            implementationClass = 'com.project.starter.modules.plugins.AndroidApplicationPlugin'
        }
        commonConfig {
            id = 'com.starter.config'
            displayName = 'Common Configuration Plugin'
            implementationClass = 'com.project.starter.config.plugins.CommonSettingsPlugin'
        }
    }
}
