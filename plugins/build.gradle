plugins {
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'java-gradle-plugin'
    id 'com.starter.library.kotlin'
    id 'maven-publish'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.6'
}

repositories {
    jcenter()
    google()
    gradlePluginPortal()
}

tasks.named('lintKotlinMain') {
    dependsOn 'formatKotlin'
}

dependencies {
    api('org.jmailen.gradle:kotlinter-gradle:2.3.2') {
        // https://github.com/pinterest/ktlint/issues/678
        exclude group: 'com.pinterest.ktlint', module: 'ktlint-test'
    }
    api 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.8.0'
    api 'com.android.tools.build:gradle:3.6.3'
    api 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    implementation 'pl.allegro.tech.build:axion-release-plugin:1.11.0'
    implementation project(":issuechecker")

    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
}

jacoco {
    toolVersion = "0.8.5"
}

java {
    withSourcesJar()
}
test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}

publishing {
    repositories {
        maven {
            name = 'github'
            setUrl('https://maven.pkg.github.com/mateuszkwiecinski/project-starter')
            credentials {
                username 'mateuszkwiecinski'
                password project.findProperty('GH_TOKEN') ?: ''
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
pluginBundle {
    website = 'https://github.com/mateuszkwiecinski/project-starter/'
    vcsUrl = 'https://github.com/mateuszkwiecinski/project-starter/'
    description = 'Set of plugins that might be useful for Multi-Module Android projects.'
    tags = ['android', 'kotlin', 'quickstart', 'codestyle', 'library', 'baseline']
}

gradlePlugin {
    plugins {
        quality {
            id = 'com.starter.quality'
            displayName = 'Code Quality Plugin'
            implementationClass = 'com.project.starter.quality.plugins.QualityPlugin'
        }
        kotlinLibrary {
            id = 'com.starter.library.kotlin'
            displayName = 'Kotlin Library Plugin'
            implementationClass = 'com.project.starter.modules.plugins.KotlinLibraryPlugin'
        }
        androidLibrary {
            id = 'com.starter.library.android'
            displayName = 'Android Library Plugin'
            implementationClass = 'com.project.starter.modules.plugins.AndroidLibraryPlugin'
        }
        androidApplication {
            id = 'com.starter.application.android'
            displayName = 'Android Applicataion Plugin'
            implementationClass = 'com.project.starter.modules.plugins.AndroidApplicationPlugin'
        }
        commonConfig {
            id = 'com.starter.config'
            displayName = 'Common Configuration Plugin'
            implementationClass = 'com.project.starter.config.plugins.CommonSettingsPlugin'
        }
        versioning {
            id = 'com.starter.versioning'
            displayName = 'Versioning Plugin'
            implementationClass = 'com.project.starter.versioning.plugins.VersioningPlugin'
        }
    }
}
