import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'maven-publish'
    id 'org.jmailen.kotlinter' version '2.2.0'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.4'
    id 'jacoco'
}

repositories {
    jcenter()
    google()
    gradlePluginPortal()
}

tasks.named("jacocoTestReport") {
    dependsOn "test", "generateJacocoTestKitProperties"
}

tasks.withType(KotlinCompile) {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    implementation 'org.jmailen.gradle:kotlinter-gradle:2.2.0'
    implementation 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.3.1'
    implementation 'com.android.tools.build:gradle:3.5.3'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation 'org.assertj:assertj-core:3.14.0'
}

publishing {
    repositories {
        maven {
            name = "github"
            setUrl('https://maven.pkg.github.com/mateuszkwiecinski/project-starter')
            credentials{
                username 'mateuszkwiecinski'
                password project.findProperty("GH_TOKEN") ?: ""
            }
        }
    }
    publications {
        register("jar", MavenPublication) {
            from(components.java)
        }
    }
}
gradlePlugin {
    plugins {
        quality {
            id = 'plugin-quality'
            implementationClass = 'com.project.starter.quality.plugins.QualityPlugin'
        }
        kotlinLibrary {
            id = 'plugin-library.kotlin'
            implementationClass = 'com.project.starter.modules.plugins.KotlinLibraryPlugin'
        }
        androidLibrary {
            id = 'plugin-library.android'
            implementationClass = "com.project.starter.modules.plugins.AndroidLibraryPlugin"
        }
        commonConfig {
            id = "plugin-config"
            implementationClass = 'com.project.starter.config.plugins.CommonSettingsPlugin'
        }
    }
}
